command /commandspy [<player>]:
	aliases: /commands, /cspy
	permission: command.spy
	permission message: &4&lI'm sorry, but you do not have the permission to execute that command.
	trigger:
		if arg 1 is set:
			if {cs::%uuid of arg 1%} is set:
				clear {cs::%uuid of arg 1%}
				send "&4Command spy has been disabled for &3%arg 1%."
				send "&4Your command spy has been disabled." to arg 1
			else:
				set {cs::%uuid of arg 1%} to true
				send "&4Your command spy has been enabled." to arg 1
				send "&4Command spy has been enabled for &3%arg 1%."
		else:
			if {cs::%uuid of player%} is set:
				clear {cs::%uuid of player%}
				send "&c&l(!) Your command spy has been disabled."
			else:
				set {cs::%uuid of player%} to true
				send "&a&l(!) Your command spy has been enabled."
on command:
	if sender is a player:
		loop all players:
			if {cs::%uuid of loop-player%} is set:
				if "%player%" is not "%loop-player%":
					send "&4&l%player% executed the command  &3&n/%complete command%&c&l." to loop-player